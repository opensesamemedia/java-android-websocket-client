apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.6
targetCompatibility = 1.6

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties"))) // Set env variable GPR_USER & GPR_API_KEY if not adding a properties file


def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    developers {
        developer {
            name "Gustavo Avila"
            email "gusavila92@gmail.com"
        }
    }
    scm {
    	connection "scm:git:git://github.com/gusavila92/java-android-websocket-client.git"
    	developerConnection "scm:git:ssh://github.com:gusavila92/java-android-websocket-client.git"
    	url "http://github.com/gusavila92/java-android-websocket-client/tree/master"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'dev.gustavoavila'
            artifactId 'java-android-websocket-client'
            version '2.0.1-osm'
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Java/Android WebSocket Client')
                root.appendNode('description', 'A very lightweight WebSocket client library for Java/Android which aims to implement the WebSocket protocol as defined in RFC 6455')
                root.appendNode('url', 'https://github.com/gusavila92/java-android-websocket-client')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            url "https://maven.pkg.github.com/opensesamemedia/java-android-websocket-client"
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

